plugins {
	id 'fabric-loom' version "${loom_version}"
	id 'maven-publish'
}

String getGitVersion() {
	Boolean isPreRelease = true
	String preReleaseProperty = findProperty("prerelease") ?: ""
	if (!preReleaseProperty.isEmpty() && preReleaseProperty.toLowerCase() == "false") {
		isPreRelease = false
	}
	String ciVersionProperty = findProperty("semVer") ?: ""
	String modLoader = findProperty("mod_loader")
	String gameVersion = findProperty("minecraft_version")
	
	String buildVersion
	if (ciVersionProperty != "") {
		// In CI, this property is set to versionize
		String ciVersion = gameVersion + "-" + ciVersionProperty
		buildVersion = modLoader + "-" + ciVersion
	}
	else {
		buildVersion = modLoader + "-" + getVersionFromGitVersionOrTag(gameVersion)
	}
	
	return isPreRelease ? buildVersion + "-preview" : buildVersion
}

version = getGitVersion()
group = project.maven_group


base {
	archivesName = project.archives_base_name
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	// You should only use this when depending on other mods because
	// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
	// See https://docs.gradle.org/current/userguide/declaring_repositories.html
	// for more information about repositories.
}

loom {
	splitEnvironmentSourceSets()

	mods {
		"armor-hider" {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}

}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${property("yarn_mappings")}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	testImplementation(platform('org.junit:junit-bom:6.0.1'))
	testImplementation('org.junit.jupiter:junit-jupiter')
	testRuntimeOnly('org.junit.platform:junit-platform-launcher')
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": inputs.properties.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_23
	targetCompatibility = JavaVersion.VERSION_23
}

jar {
	inputs.property "archivesName", project.base.archivesName

	from("LICENSE") {
		rename { "${it}_${inputs.properties.archivesName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		create("mavenJava", MavenPublication) {
			artifactId = project.archives_base_name
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

static String getVersionFromGitVersionOrTag(String gameVersion){
	String semVer
	String commitsSinceSource
	String uncommittedChanges
	try {
		try{
			semVer = "gitversion /output json /showVariable majorMinorPatch".execute().text.trim().replace("\n", "").replace("\r", "")
			commitsSinceSource = "gitversion /output json /showVariable CommitsSinceVersionSource".execute().text.trim().replace("\n", "").replace("\r", "")
			uncommittedChanges = "gitversion /output json /showVariable UncommittedChanges".execute().text.trim().replace("\n", "").replace("\r", "")
		} catch (ignored) {
			semVer = "dotnet gitversion /output json /showVariable majorMinorPatch".execute().text.trim().replace("\n", "").replace("\r", "")
			commitsSinceSource = "dotnet gitversion /output json /showVariable CommitsSinceVersionSource".execute().text.trim().replace("\n", "").replace("\r", "")
			uncommittedChanges = "dotnet gitversion /output json /showVariable UncommittedChanges".execute().text.trim().replace("\n", "").replace("\r", "")
		}
		if (semVer.isEmpty() || !semVer.matches(".*\\d.*") || !commitsSinceSource.matches("\\d.*")){
			throw new Exception()
		}
		if (commitsSinceSource == "0" && uncommittedChanges == "0")
		{
			return gameVersion + "-" + semVer
		}
		else if (commitsSinceSource == "0" && uncommittedChanges != "0"){
			return gameVersion + "-" + semVer + "-" + uncommittedChanges
		}
		else {
			return gameVersion + "-" + semVer + "-" + commitsSinceSource
		}
	}
	catch (ignored) {
		String lastKnownTag = "git describe --tags".execute().text.trim()
		String version
		if (lastKnownTag.contains(gameVersion)) {
			version = lastKnownTag
		}
		else {
			version = gameVersion + "-" + lastKnownTag
		}
		return version
	}
}